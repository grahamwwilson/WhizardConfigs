########################################################################
#
# Copyright (C) 1999-2017 by 
#     Wolfgang Kilian <kilian@physik.uni-siegen.de>
#     Thorsten Ohl <ohl@physik.uni-wuerzburg.de>
#     Juergen Reuter <juergen.reuter@desy.de>
#     with contributions from
#     Christian Speckner <cnspeckn@googlemail.com>
#
# WHIZARD is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by 
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# WHIZARD is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
########################################################################

model = SM_CKM
mW = 80.369
mH = 125.2

alias parton = u:U:d:D:s:S:c:C:g

?write_raw=false
sample_format = lhef
#?vis_diags = true

# For now only two generations of leptons
alias lepton = e1:e2
alias antilepton = E1:E2
alias neutrino = n1:n2
alias antineutrino = N1:N2

# Do W- Z production and W+ Z in the tri-lepton channels 
# Here require Z -> tau+ tau-.

process WZPlus  = parton, parton => E1 + E2, n1 + n2, e3, E3
process WZMinus = parton, parton => e1 + e2, N1 + N2, e3, E3

# Need to set s,c quark masses to zero for this to work.
ms = 0
mc = 0
# Note in this case we retain the electron and muon masses

?ps_fsr_active = true
?hadronization_active = false
$shower_method = "PYTHIA6"
?ps_taudec_active = true
polarized e3, E3, "W+", "W-", Z
?polarized_events = true 
#ps_tauola_dec_mode1=1
#ps_tauola_dec_mode2=1

compile

sqrts = 13600.0
beams = p, p => lhapdf
#$pdf_builtin_set = "cteq6l" 

cuts = all M > 4.0 GeV [combine [e1, E1]] 
       and all M > 4.0 GeV [combine [e2, E2]] 
       
$integration_method = "vamp2"       
integrate (WZPlus, WZMinus) { iterations = 10:40000:"gw", 5:80000:"" }
n_events = 100000

# Define title etc. as global variables, that will be used by PLOT
$description =
  "$\mathrm{p} \mathrm{p} \to \mu^{+} \nu_{\mu} e^{+} e^{-} $ at $\sqrt{s} = 13.6$~TeV"
$y_label = "$N_{\textrm{events}}$"

# Allocate plots

$title = "Cos Theta ($e^{-}$)"
$x_label = "$\cos{\theta_{e^{-}}}$"
histogram costhe (-1.0, 1.0, 0.1)

$title = "Cos Theta ($e^{+}$)"
$x_label = "$\cos{\theta_{e^{+}}}$"
histogram costhp (-1.0, 1.0, 0.1)

$title = "Cos Theta ($\mu^{+}$)"
$x_label = "$\cos{\theta_{\mu^{+}}}$"
histogram costhm (-1.0, 1.0, 0.1)

$title = "Cos Theta ($\nu_{\mu}$)"
$x_label = "$\cos{\theta_{\nu_{\mu}}}$"
histogram costhn (-1.0, 1.0, 0.1)

$title = "Eta ($e^{-}$)"
$x_label = "$\eta_{e^{-}}$"
histogram etae (-7.5, 7.5, 0.1)

$title = "Eta ($e^{+}$)"
$x_label = "$\eta_{e^{+}}$"
histogram etap (-7.5, 7.5, 0.1)

$title = "Eta ($\mu^{+}$)"
$x_label = "$\eta_{\mu^{+}}$"
histogram etam (-7.5, 7.5, 0.1)

$title = "Eta ($\nu_{\mu}$)"
$x_label = "$\eta_{\nu_{\mu}}$"
histogram etan (-7.5, 7.5, 0.1)

$title = "Dielectron Mass"
$x_label = "$m_{e^{+}e^{-}}$ [GeV]"
histogram mdiel (0.0, 250.0, 2.5)

$title = "Dielectron pT"
$x_label = "$p_{T} (e^{+}e^{-})$ [GeV]"
histogram ptdiel (0.0, 250.0, 2.5)

$title = "Muon-Neutrino Mass"
$x_label = "$m_{\mu^{+} \nu_{\mu}}$ [GeV]"
histogram mmunu (0.0, 250.0, 2.5)

$title = "Trilepton Mass"
$x_label = "$m_{e^{+} e^{-} \mu^{+}}$ [GeV]"
histogram mtri (0.0, 750.0, 2.5)

$title = "Fourlepton Mass"
$x_label = "$m_{e^{+} e^{-} \mu^{+} \nu_{\mu}}$ [GeV]"
histogram mquad (0.0, 1000.0, 5.0)

$title = "Fourlepton pT"
$x_label = "$p_{T} (e^{+} e^{-} \mu^{+} \nu_{\mu})$ [GeV]"
histogram ptquad (0.0, 200.0, 2.5)

$title = "MET"
$x_label = "$\mathrm{MET}$ [GeV]"
histogram hmet (0.0, 200.0, 2.0)

analysis = record costhe (eval cos(Theta)[e1]);
           record costhp (eval cos(Theta)[E1]);
           record costhm (eval cos(Theta)[E2]); 
           record costhn (eval cos(Theta)[n2]);
           record etae (eval Eta [e1]);  
           record etap (eval Eta [E1]);  
           record etam (eval Eta [E2]);  
           record etan (eval Eta [n2]);                                                                
           record mdiel (eval M [e1, E1]);
           record ptdiel (eval Pt [combine [e1,E1]]);
           record mmunu (eval M [E2, n2]);
           record mtri (eval M [combine [e1,E1], E2]);
           record mquad (eval M [combine [e1,E1], combine [E2, n2]]);
           record ptquad (eval Pt [combine [e1,E1], combine [E2, n2]]);
           record hmet (eval Pt [n2])                               

simulate (WZPlus, WZMinus) 

compile_analysis { $out_file = "WZ.dat" }
